let Step =
      < InlineFirst : Integer
      | FlexibleInlineFirst : Integer
      | InlineWidth : Integer
      | Bindings
      | Newline
      | TryInline
      >

let Style =
      < InlineFirst : Integer
      | FlexibleInlineFirst : Integer
      | InlineWidth : Integer
      | Bindings
      | Newline
      | TryInline
      | Strategy : List Step
      >

let AlignStyle = < Normal | Align >

let AlignRule = { alignAtom : Text, alignStyle : AlignStyle }

let Special = { atom : Text, style : Style }

let defaultSpecials =
      [ { atom = "if", style = Style.InlineFirst +1 }
      , { atom = "cond", style = Style.InlineFirst +1 }
      , { atom = "define", style = Style.InlineFirst +2 }
      , { atom = "defn", style = Style.InlineFirst +2 }
      , { atom = "define-function", style = Style.InlineFirst +1 }
      , { atom = "fn", style = Style.InlineFirst +2 }
      , { atom = "let", style = Style.Bindings }
      ]

let defaultAligns =
      [ { alignAtom = "fn", alignStyle = AlignStyle.Normal }
      , { alignAtom = "let", alignStyle = AlignStyle.Align }
      , { alignAtom = "define-function", alignStyle = AlignStyle.Normal }
      , { alignAtom = "defn", alignStyle = AlignStyle.Normal }
      ]

let LispFormatConfig =
      { Type =
          { indentWidth : Integer
          , inlineMaxWidth : Integer
          , defaultStyle : Style
          , defaultAlign : AlignStyle
          , specials : List Special
          , aligns : List AlignRule
          , preserveBlankLines : Bool
          }
      , default =
        { specials = defaultSpecials
        , aligns = defaultAligns
        , preserveBlankLines = True
        }
      }

in  { Step, Style, AlignStyle, AlignRule, Special, LispFormatConfig }
